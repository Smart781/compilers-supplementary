-- Expression evaluator

import List;
import State;
import World;
import Lexer;
import Parser;

-- As association map which maps "\otimes" into "\oplus"
var ops = {
  ["+" , infix + ],
  ["-" , infix - ],
  ["*" , infix * ],
  ["/" , infix / ],
  ["%" , infix % ],
  ["==", infix ==],
  ["!=", infix !=],
  ["<" , infix < ],
  ["<=", infix <=],
  [">" , infix > ],
  [">=", infix >=],
  ["&&", infix &&],
  ["!!", infix !!]
};

-- The evaluator for infix operators: takes an operator as a string
-- and two operand values, and returns the result
public fun evalOp (op, l, r) {
  case assoc (ops, op) of
    Some (f) -> f (l, r)
  esac
}

-- Evaluates an expression "expr" in a configuration "c".
-- A configuration is a pair of a state "s" and a world "w".
-- Returns a final configuration (if any)
--
-- A expression is represented by a data structure of the following shape:
--
-- expr = Assn    (string, expr)       |
--        Seq     (expr, expr)         |
--        Skip                         |
--        Read    (string)             |
--        Write   (expr)               |
--        If      (expr, expr, expr)   |
--        While   (expr, expr)         |
--        DoWhile (expr, expr)         |
--        Var     (string)             |
--        Ref     (string)             |
--        Const   (int)                |
--        Binop   (string, expr, expr) |
--        Call    (string, expr list)  |
--        Ignore  (expr)

-- Helper function: checks that given name designates a regular variable in
-- a given state
fun checkVar (state, name) {
  case state.lookup (name) of
    Var (_) -> skip
  | _       -> error (sprintf ("the name ""%s"" does not designate a variable", name), getLoc (name))
  esac
}

-- Helper function: checks that given name designates a function in
-- a given state
fun checkFun (state, name) {
  case state.lookup (name) of
    Fun (_, _) -> skip
  | _          -> error (sprintf ("the name ""%s"" does not designate a function", name), getLoc (name))
  esac
}

-- Helper function: adds a bunch of regular variables current scope
fun addNames (state, names) {
  foldl (fun (s, name) {s.addName (name, Var (0))}, state, names)
}

fun add_name (s, expr) {
  case expr of
    Var (x) -> addNames (s, x)
    | Fun (x, args, body) -> addFunction (s, x, args, body)
  esac
}

-- Helper function: adds a function in current scope
fun addFunction (state, name, args, body) {
  state.addName (name, Fun (args, body))
}
                                        
-- Evaluates a list of expressions, properly threading a configurations.
-- Returns the final configuration and the list of values
fun evalList (c, exprs) {
  case foldl (fun ([c, vals], e) {
                case eval (c, e) of
                  [c, v] -> [c, v : vals]
                esac
              },
              [c, {}],
              exprs) of
    [c, vals] -> [c, reverse (vals)]
  esac
}

(* Assignment *)
fun eval (c@[s, w], expr) {
  case expr of
    Read (x) ->
      case readWorld (w) of
        [tmp, w] -> [[s <- [x, tmp], w], {}]
      esac
    | Write (exp) ->
      case eval (c, exp) of
        [[s, w], value] -> [[s, writeWorld (value, w)], {}]
      esac
    | Skip -> [c, {}]
    | Const (n) -> [c, n]
    | Var (x) -> [c, lookup (s, x)]
    | Ref (refExp) -> [c, refExp]
    | Binop (op, leftExp, rightExp) ->
      case evalList (c, {leftExp, rightExp}) of
        [c, {leftExp, rightExp}] -> [c, evalOp (op, leftExp, rightExp)]
      esac
    | Ignore (ignoredExp) ->
      case eval (c, ignoredExp) of
        [c, _] -> [c, {}]
      esac
    | Assn (variable, valueExp) ->
      case evalList (c, {variable, valueExp}) of
        [[s, w], {variable, valueExp}] -> [[s <- [variable, valueExp], w], valueExp]
      esac
    | If (cond, tb, elb) ->
      case eval (c, cond) of [c, res] ->
        if res
            then eval (c, tb)
            else eval (c, elb)
        fi
      esac
    | DoWhile (cond, dwb) -> eval (c, Seq (cond, While (dwb, cond)))
    | While (cond, wb) ->
      case eval (c, cond) of [c, res] ->
        if res
            then eval (c, Seq (wb, While (cond, wb)))
            else [c, {}]
        fi
      esac
    | Seq (firstExp, secondExp) ->
      case evalList (c, {firstExp, secondExp}) of
        [c, {firstExp, secondExp}] -> [c, secondExp]
      esac
    | Scope (defs, expr) ->
        case s.enterScope of
          st1 -> case foldl (add_name, st1, defs) of 
            s -> case eval ([s, w], expr) of
              [c@[s, w], result] -> [[s.leaveScope, w], result]
            esac
          esac
        esac
    | Call (name, args) -> 
        case evalList ([s, w], args) of
          [[s1, w1], argVals] -> 
            case lookup (s1, name) of
              Fun (params, body) -> 
                case enterFunction (s1) of 
                  funcScope -> case foldl (fun (s, [name, value]) { s.addName (name, value) }, funcScope, zip (params, argVals)) of 
                    funcScope1 -> case eval ([funcScope1, w1], body) of 
                      [[funcScope2, finalWorld], result] -> [[leaveFunction (s1, getGlobal (funcScope2)), finalWorld], result]
                    esac
                  esac
                esac
            esac
        esac
    esac
}
(* End *)

-- Evaluates a program with a given input and returns an output
public fun evalExpr (input, expr) {
  case eval ([emptyState (), createWorld (input)], expr) of
    [c, _] -> c.snd.getOutput
  esac
}
